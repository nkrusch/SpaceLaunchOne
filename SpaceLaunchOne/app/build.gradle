apply plugin: 'com.android.application'

android {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('secret.properties').newDataInputStream())
    Properties versionProps = new Properties()
    def versionPropsFile = project.rootProject.file('version.properties')
    versionProps.load(new FileInputStream(versionPropsFile))

    def major = 3
    def minor = 1
    def patch = 1
    def code = versionProps['VERSION_CODE'].toInteger() + 1
    versionProps['VERSION_CODE'] = code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    signingConfigs {
        config {
            storeFile file(properties.getProperty('storeFile'))
            keyAlias properties.getProperty('keyAlias')
            storePassword properties.getProperty('storePassword')
            keyPassword properties.getProperty('keyPassword')
        }
    }
    compileSdkVersion 31
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "io.github.nkrusch.spacelaunchone"
        minSdkVersion 19
        targetSdkVersion 31
        versionCode code
        multiDexEnabled true
        versionName major + "." + minor + "." + patch
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'int', "UpdateInterval", Min_Update_Interval
        buildConfigField 'int', "MissionTextClipLength", MissionText_ClipLength
        buildConfigField 'String', "CloudinaryClound", properties.getProperty('Cloudinary_Cloud')
        resValue("string", "google_maps_key", properties.getProperty('Google_Maps_Key'))
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    flavorDimensions "distribution"
    productFlavors {
        playstore {
            dimension "distribution"
            buildConfigField 'String', "betaURL", Google_play_beta_URL
            buildConfigField 'String', "rateURL", Google_play_store_rate_URL
        }
        amazonAppstore {
            dimension "distribution"
            buildConfigField 'String', "betaURL", Amazon_appstore_beta_URL
            buildConfigField 'String', "rateURL", Amazon_appstore_rate_URL
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // android + support libraries
    implementation "androidx.legacy:legacy-support-v4:$legacySupportVersion"
    implementation "androidx.vectordrawable:vectordrawable:$vectorVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "androidx.cardview:cardview:$cardViewVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.preference:preference:$androidxVersion"
    implementation "androidx.fragment:fragment:$fragmentVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.multidex:multidex:$multidexVersion"

    // 3rd party libs
    implementation "com.squareup.picasso:picasso:$picassoVersion"
    implementation "com.cloudinary:cloudinary-android:$cloudinaryVersion"
    implementation "com.google.android.gms:play-services-maps:$googleMapsVersion"
    implementation "com.google.android.gms:play-services-gcm:$googlegcmVersion"

    // project modules
    implementation project(':datasrc')
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.addStringOption("encoding", "UTF-8")
    options.addStringOption("charset", "UTF-8")
    destinationDir = file("../../javadoc/")
    failOnError false
}
